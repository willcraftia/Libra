cbuffer PerRenderTarget : register(b0)
{
    // テクセル オフセット
    float2 Offsets[4];
};

// ダミー。
Texture2D<float> Texture : register(t0);
SamplerState TextureSampler : register(s0);

Texture2D<float> HeightMap : register(t1);
SamplerState HeightMapSampler : register(s1);

float4 PS(float4 color    : COLOR0,
          float2 texCoord : TEXCOORD0) : SV_Target0
{
    // *  - h2 - *
    // |    |    |
    // h0 - c  - h1
    // |    |    |
    // *  - h3 - *
    float h0 = HeightMap.Sample(HeightMapSampler, texCoord + Offsets[0]);
    float h1 = HeightMap.Sample(HeightMapSampler, texCoord + Offsets[1]);
    float h2 = HeightMap.Sample(HeightMapSampler, texCoord + Offsets[2]);
    float h3 = HeightMap.Sample(HeightMapSampler, texCoord + Offsets[3]);

    float3 u = float3(1, (h1 - h0) * 0.5, 0);
    float3 v = float3(0, (h3 - h2) * 0.5, 1);

    float3 normal = normalize(cross(v, u));

    return float4(normal, 0);
}
