cbuffer PerScene : register(b0)
{
    // [0.0, 1.0]
    float  Density          : packoffset(c0);

    float3 FogColor         : packoffset(c1);
    float  FarClipDistance  : packoffset(c1.w);
};

Texture2D<float4> Texture           : register(t0);
Texture2D<float>  LinearDepthMap    : register(t1);

SamplerState TextureSampler         : register(s0);
SamplerState LinearDepthMapSampler  : register(s1);

float4 PS(float2 texCoord : TEXCOORD0,
          float3 viewRay  : TEXCOORD1) : SV_Target
{
    float depth = LinearDepthMap.SampleLevel(LinearDepthMapSampler, texCoord, 0);

    float4 color = Texture.Sample(TextureSampler, texCoord);

    if (0 < FarClipDistance && FarClipDistance <= depth)
        return color;

    // 座標 (ビュー空間)
    float3 position = viewRay * depth;

    // 視点からの距離。
    float d = length(position);

    float x = d * Density;
    x *= x;

    float fogFactor = saturate(exp(-x));

    return lerp(float4(FogColor, 1), color, fogFactor);
}
